mapscripts FabledIsland_Interior_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SetupRivalGfxId
}

script SetupRivalGfxId {
    removeobject(13)
    call(Common_EventScript_SetupRivalGfxId)
}

script FabledIsland_Interior_EnterRival {
    checkplayergender
    if(var(VAR_RESULT) == MALE) {
        playbgm(MUS_ENCOUNTER_MAY, FALSE)
    } else {
        playbgm(MUS_ENCOUNTER_BRENDAN, FALSE)
    }
}

text FabledIsland_Interior_StopRightThere {
	format("Stop right there punk!")
}

text FabledIsland_Interior_BadTime {
    format("He he he, hope we caught you at a bad time!")
}

text FabledIsland_Interior_GotYouSurrounded {
    format("You may have beaten our Admins, but you can't beat all of us!\pYou're strong, but our numbers will beat you and your Pokemon down. There will be no rest for you!\pGet ready for our Ultra-Neo-Gang-Beat-Up")
}

text FabledIsland_Interior_RivalEntry_May {
    format("May: Now you hold it!")
}

text FabledIsland_Interior_RivalAssure_May {
    format("May: I saw you chasing after the Aqua grunt, but you flew off before I could reach you. I always seem to be playing catch up...\pYou may be the Champion, but that doesn't mean I'm going to sit back and watch. We're still rivals after all!")
}

text FabledIsland_Interior_RivalEntry_Brendan {
    format("Brendan: Now you hold it!")
}

text FabledIsland_Interior_RivalAssure_Brendan {
    format("Brendan: I saw you chasing after the Aqua grunt, but you flew off before I could reach you. I always seem to be playing catch up...\pYou may be the Champion, but that doesn't mean I'm going to sit back and watch. We're still rivals after all!")
}

text FabledIsland_Interior_ItDoesntMatter {
    format("Neo Grunt: Another one won't make a difference, we've got this, now let's stomp them!\pGrunts: YEAAAAAHHH!!!!")
}

text FabledIsland_Interior_LoseText1 {
    format("Tch, I had to go first")
}

text FabledIsland_Interior_LoseText2 {
    format("Haha, well your friend there isn't looking too good")
}

text FabledIsland_Interior_Rival_May_Tired {
    format("May: Hey {PLAYER}, I'm sorry, I made a critical mistake in that last fight.\pI need some time to heal, please hold them off for me.")
}

text FabledIsland_Interior_Rival_Brendan_Tired {
    format("Brendan: Hey {PLAYER}, I'm sorry, I made a critical mistake in that last fight.\pI need some time to heal, please hold them off for me.")
}

text FabledIsland_Interior_LoseText3 {
    format("No way, I think we blew our chance to win")
}

script FabledIsland_Interior_EventScript_Horde {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Walk_To_Center)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    playse(SE_PIN)
    addobject(1)
    addobject(2)
    addobject(3)
    addobject(4)
    addobject(5)
    addobject(6)
    addobject(7)
    addobject(8)
    addobject(9)
    addobject(10)
    addobject(11)
    addobject(12)
    addobject(13)
    msgbox(FabledIsland_Interior_StopRightThere, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerFaceUp)  // have grunts enter scene
    applymovement(1, Horde_Grunt_Magma_1)
    applymovement(2, Horde_Grunt_Aqua_2)
    applymovement(3, Horde_Grunt_Aqua_3)
    applymovement(4, Horde_Grunt_Magma_4)
    applymovement(5, Horde_Grunt_Aqua_5)
    applymovement(6, Horde_Grunt_Magma_6)
    applymovement(7, Horde_Grunt_Magma_7)
    applymovement(8, Horde_Grunt_Aqua_8)
    applymovement(9, Horde_Grunt_Magma_9)
    applymovement(10, Horde_Grunt_Aqua_10)
    applymovement(11, Horde_Grunt_Aqua_11)
    applymovement(12, Horde_Grunt_Magma_12)
    waitmovement(12)
    msgbox(FabledIsland_Interior_BadTime, MSGBOX_DEFAULT)
    msgbox(FabledIsland_Interior_GotYouSurrounded, MSGBOX_DEFAULT)
    checkplayergender //0 is male, 1 is female
    if (var(VAR_RESULT) == 0) { // if rival is may
        msgbox(FabledIsland_Interior_RivalEntry_May, MSGBOX_DEFAULT)
        closemessage
        applymovement(13, Rival_Walk_To_Center)
        waitmovement(13)
        msgbox(FabledIsland_Interior_RivalAssure_May, MSGBOX_DEFAULT)
        applymovement(9, Horde_Grunt_Talk_Movement_9)
        msgbox(FabledIsland_Interior_ItDoesntMatter, MSGBOX_DEFAULT)
    } else {
        msgbox(FabledIsland_Interior_RivalEntry_Brendan, MSGBOX_DEFAULT)
        closemessage
        applymovement(13, Rival_Walk_To_Center)
        waitmovement(13)
        msgbox(FabledIsland_Interior_RivalAssure_Brendan, MSGBOX_DEFAULT)
        applymovement(9, Horde_Grunt_Talk_Movement_9)
        msgbox(FabledIsland_Interior_ItDoesntMatter, MSGBOX_DEFAULT)
    }
    applymovement(1, Horde_Grunt_1_Approach)
    applymovement(2, Horde_Grunt_2_Approach)
    waitmovement(1) // 1 has the most movement so it would take the longest 
    trainerbattle_no_intro(TRAINER_GRUNT_MAGMA_HIDEOUT_10, FabledIsland_Interior_LoseText1)
    applymovement(1, Horde_Grunt_1_Retreat)
    applymovement(2, Horde_Grunt_2_Retreat)
    waitmovement(1)
    applymovement(3, Horde_Grunt_3_Approach)
    applymovement(4, Horde_Grunt_4_Approach)
    waitmovement(4)
    trainerbattle_no_intro(TRAINER_GRUNT_SEAFLOOR_CAVERN_1, FabledIsland_Interior_LoseText2)
    applymovement(3, Horde_Grunt_3_Retreat)
    applymovement(4, Horde_Grunt_4_Retreat)
    waitmovement(4)
    checkplayergender //0 is male, 1 is female
    if (var(VAR_RESULT) == 0) { // if rival is may
        msgbox(FabledIsland_Interior_Rival_May_Tired, MSGBOX_DEFAULT)
        closemessage
    } else {
        msgbox(FabledIsland_Interior_Rival_Brendan_Tired, MSGBOX_DEFAULT)
        closemessage
    }
    applymovement(5, Horde_Grunt_5_Approach)
    applymovement(6, Horde_Grunt_6_Approach)
    waitmovement(6)
    trainerbattle_no_intro(TRAINER_GRUNT_SEAFLOOR_CAVERN_2, FabledIsland_Interior_LoseText3)
    applymovement(5, Horde_Grunt_5_Retreat)
    applymovement(6, Horde_Grunt_6_Retreat)
    waitmovement(6)
}

movement Movement_Walk_To_Center {
    walk_up * 8
    emote_exclamation_mark
    face_down
}

movement PlayerFaceUp {
    delay_16 * 4
    face_up
}

movement Horde_Grunt_Talk_Movement_9 {
    walk_in_place_down
}

movement Horde_Grunt_Magma_1 { //obj id 1
    walk_fast_up * 4
    walk_fast_left * 5
    walk_fast_up * 6
    walk_fast_right
}

movement Horde_Grunt_Aqua_2 { //obj id 2
    walk_in_place_up
    walk_fast_up * 4
    walk_fast_right * 4
    walk_fast_up * 3
    walk_fast_left * 1
    walk_in_place_left
}

movement Horde_Grunt_Aqua_3 { //obj id 3
    walk_in_place_up * 2
    walk_fast_up * 4
    walk_fast_right * 4
    walk_fast_up * 7
    walk_fast_left * 2
    walk_in_place_down
}

movement Horde_Grunt_Magma_4 { //obj id 4
    walk_in_place_up * 3
    walk_fast_up * 4
    walk_fast_left * 3
    walk_fast_up * 2
}

movement Horde_Grunt_Aqua_5 { //obj id 5
    walk_in_place_up * 4
    walk_fast_up * 4
    walk_fast_left * 3
    walk_fast_up * 7
    walk_in_place_down
}

movement Horde_Grunt_Magma_6 { //obj id 6 woman
    walk_in_place_up * 5
    walk_fast_up * 4
    walk_fast_left * 4
    walk_fast_up * 3
    walk_in_place_right
}

movement Horde_Grunt_Magma_7 { //obj id 7
    walk_in_place_up * 6
    walk_fast_up * 5
    walk_fast_left
    walk_in_place_up
}

movement Horde_Grunt_Aqua_8 { //obj id 8 woman
    walk_in_place_up * 7
    walk_fast_up * 4
    walk_fast_right *4
    walk_fast_up * 8
    walk_fast_left * 4
    walk_in_place_down
}

movement Horde_Grunt_Magma_9 { //obj id 9
    walk_in_place_up * 8
    walk_fast_up * 4
    walk_fast_left * 2
    walk_fast_up * 8
    walk_fast_right
    walk_in_place_down
}

movement Horde_Grunt_Aqua_10 { //obj id 10 woman
    walk_in_place_up * 9
    walk_fast_up * 5
}

movement Horde_Grunt_Aqua_11 { //obj id 11 
    walk_in_place_up * 10
    walk_fast_up * 4
    walk_fast_right * 4
    walk_fast_up * 6
    walk_fast_left * 1
}

movement Horde_Grunt_Magma_12 { //obj id 12 woman
    walk_in_place_up * 11
    walk_fast_up * 4
    walk_fast_right * 2
    walk_fast_up * 2
}

movement Rival_Walk_To_Center { //obj id 13
    walk_fast_up * 3
    walk_fast_right 
    walk_fast_up * 5
    walk_fast_left
    walk_in_place_down
}

movement Horde_Grunt_1_Approach {
    walk_right * 4
    walk_in_place_down
}

movement Horde_Grunt_1_Retreat {
    walk_left * 4
    walk_in_place_right
}

movement Horde_Grunt_2_Approach {
    walk_left * 3
    walk_in_place_up
}

movement Horde_Grunt_2_Retreat {
    walk_right * 3
    walk_in_place_left
}

movement Horde_Grunt_3_Approach {
    walk_left * 2
    walk_down
}

movement Horde_Grunt_4_Approach {
    walk_right * 3
    walk_up
}

movement Horde_Grunt_3_Retreat {
    walk_up
    walk_right * 2
    face_down
}

movement Horde_Grunt_4_Retreat {
    walk_down
    walk_left * 3
    face_up
}

movement Horde_Grunt_5_Approach {
    walk_down
    walk_right * 3
    face_down
}

movement Horde_Grunt_5_Retreat {
    walk_left * 3
    walk_up
    face_down
}

movement Horde_Grunt_6_Approach {
    walk_right * 2
    walk_up * 2
    walk_right 
}

movement Horde_Grunt_6_Retreat {
    walk_left * 2
    walk_down * 2
    walk_left
    face_right 
}
