mapscripts FabledIsland_Interior_Summit_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SetupRival_Summit_GfxId
}

script SetupRival_Summit_GfxId {
    removeobject(4)
    if(flag(FLAG_NEO_EVENT_DONE)) {
        call(Common_EventScript_SetupRivalGfxId)
        addobject(4)
    }
}

text FabledIsland_Summit_Maxie_Finally {
    format("Maxie: So, you've finally come.\pHehehe, you're too late. Archie and I have already captured the POKéMON from the meteor")
}

text FabledIsland_Summit_Archie_Jirachi {
    format("Archie: Fufufu, we caught JIRACHI, and used its power to wish for a POKéMON to defeat you!\pIt granted us this DEOXYS, and with it we will defeat you easily!")
}

text FabledIsland_Summit_Maxie_Wait {
    format("Maxie: Wait, though it would be easy to defeat {PLAYER} here, with their tired POKéMON, it wouldn't be ideal.\pWe should let {PLAYER} heal and fight us in our base, the height of our power verses theirs, for dominance of Hoenn.")
}

text FabledIsland_Summit_Archie_Good_Idea {
    format("Archie: Heh, not a bad idea. That would give us time to understand DEOXYS and JIRACHI's power better too.")
}

text FabledIsland_Summit_Archie_Alright {
    format("Alright then {PLAYER}, hurry to our base near MT. PYRE, we'll be waiting for your challenge!\pTeam Neo, move out!")
}

text FabledIsland_Summit_May_Hello {
    format("May: Aaahh, the air up here is so nice! What do you think, {PLAYER}?\pI've been training hard, and I think I'm finally ready to challenge you. What do you say, are you ready to face me?")
}

text FabledIsland_Summit_May_Yes {
    format("May: Alright... this is it, I won't lose anymore!")
}

text FabledIsland_Summit_May_No {
    format("May: Haha, no worries, you don't have to prove anything to me.")
}

text FabledIsland_Summit_May_Defeated {
    format("May: I can't believe it... you're like an insurmountable wall that no one can overcome.\pBut you know what, that's okay. You've helped push me to levels farther than I could have imagined.\pI guess that's how you got so strong too, taking on gyms, the Elite Four, even the Champion.\pYour battles were incredible.\p...\pWhat do you think you'll do next? Complete your POKéDEX? Challenge the Battle Frontier? Or maybe you're planning to go abroad?\p No matter what you do, I'll be rooting for you!")
}

text FabledIsland_Summit_Brendan_Hello {
    format("Brendan: The air up here is really clear, what do you think, {PLAYER}?\pI've been training hard, and I'm ready to challenge you. What do you say, are you ready to face me?")
}

text FabledIsland_Summit_Brendan_Yes {
    format("Brendan: This is it, I won't lose to anyone!")
}

text FabledIsland_Summit_Brendan_No {
    format("Brendan: Haha, I'd be scared of my strength too.")
}

text FabledIsland_Summit_Rival_Lose_Text {
    format("Wow, that was an incredible final battle!")
}

text FabledIsland_Summit_Brendan_Defeated {
    format("Brendan: I can't believe it... you're like an insurmountable wall that no one can overcome.\pBut you know what, that's okay. You've pushed me to levels farther than I could have imagined.\pI guess that's how you got so strong too, taking on gyms, the Elite Four, even the Champion.\pYour battles were incredible.\p...\pWhat do you think you'll do next? Complete your POKéDEX? Challenge the Battle Frontier? Or maybe you're planning to go abroad?\p No matter what you do, I'll be ready for your next challenge!")
}

script FabledIsland_Summit_EventScript_Fix_Left {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, FabledIsland_Summit_Fix_Left)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    goto(FabledIsland_Summit_EventScript)
}

script FabledIsland_Summit_EventScript_Fix_Right {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, FabledIsland_Summit_Fix_Right)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    goto(FabledIsland_Summit_EventScript)
}

script FabledIsland_Summit_EventScript {
    applymovement(OBJ_EVENT_ID_PLAYER, FabledIsland_Summit_Walk_Up)
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, FabledIsland_Summit_Pan_Up)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovement(1, FabledIsland_Summit_Walk_In_Place_Down)
    msgbox(FabledIsland_Summit_Maxie_Finally, MSGBOX_DEFAULT)
    closemessage
    applymovement(2, FabledIsland_Summit_Walk_In_Place_Down)
    msgbox(FabledIsland_Summit_Archie_Jirachi, MSGBOX_DEFAULT)
    closemessage
    applymovement(1, FabledIsland_Summit_Face_Left)
    waitmovement(1)
    msgbox(FabledIsland_Summit_Maxie_Wait, MSGBOX_DEFAULT)
    applymovement(2, FabledIsland_Summit_Face_Right)
    waitmovement(2)
    msgbox(FabledIsland_Summit_Archie_Good_Idea, MSGBOX_DEFAULT)
    applymovement(1, FabledIsland_Summit_Walk_In_Place_Down)
    applymovement(2, FabledIsland_Summit_Walk_In_Place_Down)
    msgbox(FabledIsland_Summit_Archie_Alright, MSGBOX_DEFAULT)
    fadescreen(FADE_TO_BLACK)
    closemessage
    removeobject(1)
    removeobject(2)
    removeobject(3)
    removeobject(4)
    fadescreen(FADE_FROM_BLACK)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, FabledIsland_Summit_Pan_Down)
    waitmovement(0)
    special(RemoveCameraObject)
    setvar(VAR_GIFT_UNUSED_1, 2)
    setflag(FLAG_FABLED_ISLAND_FINISH)
}

script FabledIsland_Summit_Rival_Battle {
    checkplayergender //0 is male, 1 is female
    if (var(VAR_RESULT) == 0) { // if rival is may
        msgbox(FabledIsland_Summit_May_Hello, MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
            msgbox(FabledIsland_Summit_May_Yes, MSGBOX_NPC)
            //trainer battle here
            switch (var(VAR_STARTER_MON)) {
	            case 0: trainerbattle_no_intro(TRAINER_MAY_LILYCOVE_TREECKO, FabledIsland_Summit_Rival_Lose_Text)
	            case 1: trainerbattle_no_intro(TRAINER_MAY_LILYCOVE_TORCHIC, FabledIsland_Summit_Rival_Lose_Text)
	            case 2: trainerbattle_no_intro(TRAINER_MAY_LILYCOVE_MUDKIP, FabledIsland_Summit_Rival_Lose_Text)
            }
            msgbox(FabledIsland_Summit_May_Defeated)
            setflag(FLAG_FINAL_RIVAL_BATTLE)
        }
		else {
			msgbox(FabledIsland_Summit_May_No, MSGBOX_NPC)
		}
    } else { //brendan rival
        msgbox(FabledIsland_Summit_Brendan_Hello, MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
            msgbox(FabledIsland_Summit_Brendan_Yes, MSGBOX_NPC)
            //trainer battle here
            switch (var(VAR_STARTER_MON)) {
	            case 0: trainerbattle_no_intro(TRAINER_BRENDAN_LILYCOVE_TREECKO, FabledIsland_Summit_Rival_Lose_Text)
	            case 1: trainerbattle_no_intro(TRAINER_BRENDAN_LILYCOVE_TORCHIC, FabledIsland_Summit_Rival_Lose_Text)
	            case 2: trainerbattle_no_intro(TRAINER_BRENDAN_LILYCOVE_MUDKIP, FabledIsland_Summit_Rival_Lose_Text)
            }
            msgbox(FabledIsland_Summit_Brendan_Defeated)
            setflag(FLAG_FINAL_RIVAL_BATTLE)
        }
		else {
			msgbox(FabledIsland_Summit_Brendan_No, MSGBOX_NPC)
		}
    }
    closemessage
    applymovement(VAR_LAST_TALKED, FabledIsland_Summit_Walk_Fast_Down)
	waitmovement(0)
	delay(50)
	setfieldeffectargument(0, 1)
	dofieldeffect(FLDEFF_NPCFLY_OUT)
	delay(15)
	removeobject(VAR_LAST_TALKED)
	waitfieldeffect(FLDEFF_NPCFLY_OUT)
}

movement FabledIsland_Summit_Walk_In_Place_Down {
    walk_in_place_down
}

movement FabledIsland_Summit_Face_Right {
    face_right
}

movement FabledIsland_Summit_Face_Left {
    face_left
}

movement FabledIsland_Summit_Walk_Up {
    walk_up * 2
}

movement FabledIsland_Summit_Pan_Up {
    walk_slow_up * 2
}

movement FabledIsland_Summit_Pan_Down {
    walk_slow_down * 2
}

movement FabledIsland_Summit_Walk_Fast_Down {
    walk_in_place_faster_down
}

movement FabledIsland_Summit_Fix_Left {
    walk_up
    walk_right
}

movement FabledIsland_Summit_Fix_Right {
    walk_up
    walk_left
}