mapscripts AquaHideout_UnusedRubyMap2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SetupRivalGfxId_NeoHideout
}

script SetupRivalGfxId_NeoHideout {
    removeobject(3)
    call(Common_EventScript_SetupRivalGfxId)
}

text Text_NeoHideout_Map2_Archie_Youve_Come {
    format("Archie: Fufufu, you've finally made it. I had no doubts that you would.\pWas it your passion to save Hoenn?\pWas it your drive for battle?\pOr perhaps something like a love for POKéMON?")
}

text Text_NeoHideout_Map2_Maxie_Doesnt_Matter {
    format("Maxie: It doesn't matter what {PLAYER}'s reason is, they're the last obstacle that stands in our way of dominating Hoenn! Once they're out of the picture, we'll reclaim KYOGRE and GROUDON.")
}

text Text_NeoHideout_Map2_Archie_Then_We_Fight {
    format("Archie: Grahaha, and then we can go back to fighting eachother! I'll be looking forward to it.\pBut first, {PLAYER}, it's time to take you down! We're teaming up for one final battle!")
}

text Text_NeoHideout_Map2_Archie_Lose {
    format("Haha, alright Maxie you take the lead for the finish!")
}

text Text_NeoHideout_Map2_Maxie_Not_Bad {
    format("Not bad, but your strength won't be enough.")
}

text Text_NeoHideout_Map2_Maxie_Lose {
    format("...")
}

text Text_NeoHideout_Map2_Maxie_Post_Battle {
    format("Maxie: Impossible... how could we have been beaten? Even with our legendary POKéMON...")
}

text Text_NeoHideout_Map2_May_Intro { 
    format("May: Don't worry {PLAYER}, I brought backup!\pWait, you beat them already?")
}

text Text_NeoHideout_Map2_Brendan_Intro { 
    format("Brendan: Don't worry {PLAYER}, I brought backup!\pWait, you beat them already?")
}

text Text_NeoHideout_Map2_Wallace_Seems_So {
    format("Wallace: It seems so. I must say, you've gotten even stronger since we fought for your Champion title. You certainly carry it well.\pAlright, I'll take care of handing these two over to the authorities, will you come quietly?")
}

text Text_NeoHideout_Map2_Archie_Yes {
    format("Archie: Yes, we'll let our teams disband and atone for our crimes. Even JIRACHI's wish granting power couldn't give us what we needed to defeat {PLAYER}.\pBut Maxie, when we get out, let's battle again.")
}

text Text_NeoHideout_Map2_Maxie_Yes {
    format("Maxie: Haha, yes Archie, I'll be happy to face you again.")
}

text Text_NeoHideout_Map2_May_Take_Jirachi {
    format("May: Sorry I didn't make it here sooner, I thought getting help would be the best course of action.\pBut for now, I'll make sure JIRACHI gets back to Fabled Island alright. Maybe I'll spend some time training there too.")
}

text Text_NeoHideout_Map2_Brendan_Take_Jirachi {
    format("Brendan: Sorry I didn't make it here sooner, I thought getting help would be the best course of action.\pBut for now, I'll make sure JIRACHI gets back to Fabled Island alright. Maybe I'll spend some time training there too.")
}

text Text_NeoHideout_Map2_Meteor {
    format("It's a meteor. Better leave it for the authorities.")
}

script EventScript_NeoHideout_Map2_After_Centering {
    waitmovement(OBJ_EVENT_ID_PLAYER) 
    goto(EventScript_NeoHideout_Map2_Walk_To_Center)
}

script EventScript_NeoHideout_Map2_Walk_Down {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Down)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}

script EventScript_NeoHideout_Map2_Walk_Down2 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Down2)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}
script EventScript_NeoHideout_Map2_Walk_Down3 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Down3)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}
script EventScript_NeoHideout_Map2_Walk_Down4 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Down4)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}

script EventScript_NeoHideout_Map2_Walk_Up {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Up)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}

script EventScript_NeoHideout_Map2_Walk_Up2 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Up2)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}

script EventScript_NeoHideout_Map2_Walk_Up3 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Up3)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}

script EventScript_NeoHideout_Map2_Walk_Up4 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Up4)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}

script EventScript_NeoHideout_Map2_Walk_Up5 {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Up5)
    goto(EventScript_NeoHideout_Map2_After_Centering)
}


script EventScript_NeoHideout_Map2_Walk_To_Center {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Player_Walk_Center)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    goto(EventScript_NeoHideout_Map2_Final_Battle)
}
//1 is archie, 2 maxie, 3 is may, 4 is wallace
script EventScript_NeoHideout_Map2_Final_Battle {
    lockall
    applymovement(1, Movement_Walk_In_Place_Left)
    msgbox(Text_NeoHideout_Map2_Archie_Youve_Come, MSGBOX_DEFAULT)
    applymovement(2, Movement_Walk_In_Place_Left)
    msgbox(Text_NeoHideout_Map2_Maxie_Doesnt_Matter, MSGBOX_DEFAULT)
    applymovement(1, Movement_Walk_In_Place_Left)
    msgbox(Text_NeoHideout_Map2_Archie_Then_We_Fight, MSGBOX_DEFAULT)
    closemessage
    trainerbattle_no_intro(TRAINER_ARCHIE, Text_NeoHideout_Map2_Archie_Lose)
    applymovement(2, Movement_Walk_In_Place_Left)
    msgbox(Text_NeoHideout_Map2_Maxie_Not_Bad, MSGBOX_DEFAULT)
    closemessage
    trainerbattle_no_intro(TRAINER_MAXIE_MAGMA_HIDEOUT, Text_NeoHideout_Map2_Maxie_Lose)
    msgbox(Text_NeoHideout_Map2_Maxie_Post_Battle, MSGBOX_DEFAULT)
    closemessage
    addobject(3)
    addobject(4)
    applymovement(3, Movement_Backup_Approach)
    applymovement(4, Movement_Backup_Approach)
    checkplayergender //0 is male, 1 is female
    if (var(VAR_RESULT) == 0) { // if rival is may
        msgbox(Text_NeoHideout_Map2_May_Intro, MSGBOX_DEFAULT)
    } else {
        msgbox(Text_NeoHideout_Map2_Brendan_Intro, MSGBOX_DEFAULT)
    }
    msgbox(Text_NeoHideout_Map2_Wallace_Seems_So, MSGBOX_DEFAULT)
    closemessage
    applymovement(1, Movement_Walk_In_Place_Left)
    msgbox(Text_NeoHideout_Map2_Archie_Yes, MSGBOX_DEFAULT)
    closemessage
    applymovement(2, Movement_Face_Up)
    msgbox(Text_NeoHideout_Map2_Maxie_Yes, MSGBOX_DEFAULT)
    closemessage
    applymovement(1, Movement_Archie_Leave)
    applymovement(2, Movement_Maxie_Leave)
    applymovement(4, Movement_Wallace_Leave)
    waitmovement(1)
    removeobject(1)
    removeobject(2)
    removeobject(4)
    applymovement(3, Movement_Face_Down)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Face_Up)
    if (var(VAR_RESULT) == 0) { // if rival is may
        msgbox(Text_NeoHideout_Map2_May_Take_Jirachi, MSGBOX_DEFAULT)
    } else {
        msgbox(Text_NeoHideout_Map2_Brendan_Take_Jirachi, MSGBOX_DEFAULT)
    }
    closemessage
    applymovement(3, Movement_Wallace_Leave)
    waitmovement(3)
    removeobject(3)
    setflag(FLAG_NEO_EVENT_DONE)
    setvar(VAR_GIFT_UNUSED_1, 3)
    releaseall
}

script EventScript_NeoHideout_Map2_Meteor {
    lockall
    msgbox(Text_NeoHideout_Map2_Meteor, MSGBOX_SIGN)
    releaseall
}

movement Movement_Player_Walk_Down {
    walk_down
}

movement Movement_Player_Walk_Down2 {
    walk_down * 2
}

movement Movement_Player_Walk_Down3 {
    walk_down * 3
}

movement Movement_Player_Walk_Down4 {
    walk_down * 4
}

movement Movement_Player_Walk_Up {
    walk_up
}

movement Movement_Player_Walk_Up2 {
    walk_up * 2
}

movement Movement_Player_Walk_Up3 {
    walk_up * 3
}

movement Movement_Player_Walk_Up4 {
    walk_up * 4
}

movement Movement_Player_Walk_Up5 {
    walk_up * 5
}


movement Movement_Face_Down {
    face_down
}

movement Movement_Player_Walk_Center {
    walk_right * 7
}

movement Movement_Walk_In_Place_Left {
    walk_in_place_left * 2
}

movement Movement_Backup_Approach {
    walk_fast_right * 9
}

movement Movement_Face_Up {
    face_up
}

movement Movement_Wallace_Leave {
    walk_left * 9
}

movement Movement_Maxie_Leave {
    walk_down
    walk_left * 12
}

movement Movement_Archie_Leave {
    walk_down * 2
    walk_left * 12
}